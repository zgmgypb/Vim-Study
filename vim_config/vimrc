"要使用扩展很多功能，需要先设置这两个参数
set nocompatible
filetype plugin on

"设置 * 进行 visual 模式下选择的文本搜索
xnoremap * :<C-u>call <SID>VSetSearch()<CR>/<C-R>=@/<CR><CR>
xnoremap # :<C-u>call <SID>VSetSearch()<CR>?<C-R>=@/<CR><CR>
function! s:VSetSearch()
	let temp = @s
	norm! gv"sy
	let @/ = '\V' . substitute(escape(@s, '/\'), '\n', '\\n', 'g')
	let @s = temp
endfunction

"定义更新 ctags 文件
function! UpdateCtags()
	let curdir=getcwd()
	while !filereadable("./tags")
		cd ..
		if getcwd() == "/"
			break
		endif
	endwhile
	if filewritable("./tags")
		!ctags -R --file-scope=yes --langmap=c:+.h --languages=c,c++ --links=yes --c-kinds=+p --c++-kinds=+p --fields=+iaS --extra=+q
		TlistUpdate
	endif
	execute ":cd " . curdir
endfunction
"映射 <F10> 作为 ctags 更新按键
nmap <F10> :call UpdateCtags()<CR>

set nu
syntax on
set incsearch
set backspace=indent,eol,start
set autoindent
set complete=k,.

"设置命令自动补全命令方式与 bash shell 的行为一致
set wildmenu
set wildmode=longest,list

"映射命令行回溯历史按键
cnoremap <C-p> <Up>
cnoremap <C-n> <Down>

"设置多个文件的快速切换组合键映射，避免使用命令的方式
nnoremap <silent> [b :bprevious<CR>
nnoremap <silent> ]b :bnext<CR>
nnoremap <silent> [B :bfirst<CR>
nnoremap <silent> ]B :blast<CR>

"实用 %% 扩展当前缓冲区所在目录的路径
cnoremap <expr>%% getcmdtype() == ':' ? expand('%:h').'/' : '%%'

set showcmd
set ruler
set hlsearch
filetype plugin indent on
set cmdheight=2

" 设置文件编码方式自动识别打开
set fileencodings=cp936,ucs-bom,utf-8,utf-16,gbk,big5,gb18030,latin1

set nobackup
let NERDTreeWinPos='right'
map <F6> :NERDTreeToggle<CR>
map <F5> :TlistToggle<CR>

"避免出现跳转 tags 时出现重复的标签
set tags=./tags,tags

if has("cscope")
	set csprg=cscope
	set csto=0
	set cst
	set nocsverb
	if filereadable("cscope.out")
		cs add cscope.out
	elseif $CSCOPE_DB != ""
		cs add $CSCOPE_DB
	endif
	set csverb
	set cscopequickfix=s-,c-,d-,i-,t-,e-
	nmap <C-n> :cnext<CR>
	nmap <C-p> :cprev<CR>
endif

function QfMakeConv()
	let qflist = getqflist()
	for i in qflist
		let i.text = iconv(i.text, "cp936", "utf-8")
	endfor
	call setqflist(qflist)
endfunction

au QuickfixCmdPost make call QfMakeConv()
